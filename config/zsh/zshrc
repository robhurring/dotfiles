# ---> Profile
zmodload zsh/zprof

# override in ~/.zshrc before init, or whatever
export ZSH_MODE=${ZSH_MODE:-"vi"}
export ZSH_VI_ESC=${ZSH_VI_ESC:-"jk"}
export ZSH_THEME=${ZSH_THEME:-"default"}

export HISTFILE=$XDG_CACHE_HOME/zsh_history
export HISTSIZE=5000
export SAVEHIST=5000
export REPORTTIME=10

# set to speed up boot time
skip_global_compinit=${skip_global_compinit:-0}

# stty erase \^H

# see: https://zsh.sourceforge.io/Doc/Release/Options.html
setopt APPEND_HISTORY
setopt AUTO_MENU
setopt AUTO_PARAM_SLASH
setopt AUTO_REMOVE_SLASH
setopt AUTO_PUSHD
setopt COMPLETE_IN_WORD
setopt EXTENDED_HISTORY
setopt HIST_FIND_NO_DUPS
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_REDUCE_BLANKS
setopt HIST_SAVE_NO_DUPS
setopt HIST_VERIFY
setopt IGNORE_EOF
setopt INC_APPEND_HISTORY
setopt LOCAL_OPTIONS
setopt LOCAL_TRAPS
setopt NO_BG_NICE
setopt NO_HUP
setopt NO_LIST_BEEP
setopt NULL_GLOB
setopt PROMPT_SUBST
setopt PUSHD_IGNORE_DUPS
setopt SHARE_HISTORY
setopt TRANSIENT_RPROMPT

# key-bindings
bindkey '^r' history-beginning-search-backward
bindkey '^t' history-beginning-search-forward
bindkey '^?' backward-delete-char
bindkey '^h' backward-delete-char
bindkey '^w' backward-kill-word

# edit command in editor
autoload -z edit-command-line
zle -N edit-command-line
bindkey '^X^E' edit-command-line

# zsh hook
autoload -Uz add-zsh-hook

# zsh magic
autoload -U url-quote-magic
zle -N self-insert url-quote-magic

# load our config files
for lib ($ZHOME/lib/*.zsh); do
  source $lib
done

# load our shared config
for lib ($ZHOME/share/*); do
  source $lib
done

load_plugin() {
  local plugin_name=$1
  local plugin_root=$2
  local plugin_path=$plugin_root/$plugin_name

  fpath=($plugin_path $fpath)
  if [ -f $plugin_path/$plugin_name.zsh ]; then
    source $plugin_path/$plugin_name.zsh
  fi
}

# See: https://htr3n.github.io/2018/07/faster-zsh/
reload_completions() {
  zcompdump="${ZDOTDIR:-$HOME}/.zcompdump"
  if [ ! -f $zcompdump ]; then
    touch $zcompdump
  fi

  autoload -Uz compinit
  if [ $(date +'%j') != $(/usr/bin/stat -f '%Sm' -t '%j' $zcompdump) ]; then
    compinit
  else
    compinit -C
  fi
}

# run compinit
if [[ $skip_global_compinit != 1 ]]; then
  reload_completions
fi
typeset -U path manpath fpath

# load all plugins to $fpath
fpath=(/usr/local/share/zsh/site-functions $fpath)
fpath=($ZHOME/functions $fpath)
for plugin ($plugins); do
  load_plugin $plugin $ZHOME/plugins
done

if [ -f $ZSH_THEME ]; then
  source $ZSH_THEME
else
  source $ZHOME/themes/$ZSH_THEME
fi

